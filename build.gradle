plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.2.21'
    id 'org.jetbrains.kotlin.plugin.spring' version '2.2.21'
    id 'org.springframework.boot' version '4.0.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id "org.jlleitschuh.gradle.ktlint" version "14.0.1"
    id "org.jooq.jooq-codegen-gradle" version "3.20.5"
}

group = 'com.example.hkoike.codingtest'
version = '0.0.1-SNAPSHOT'
description = 'coding test'
ext["jooq.version"] = "3.20.5"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-flyway'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.flywaydb:flyway-database-postgresql'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
    implementation "org.postgresql:postgresql"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-flyway-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-jooq-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testImplementation "io.mockk:mockk:1.14.6"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    jooqCodegen "org.postgresql:postgresql"
}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll '-Xjsr305=strict', '-Xannotation-default-target=param-property'
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

ktlint {
    debug.set(false)
    verbose.set(false)
    android.set(false)
    ignoreFailures.set(false)
}

// ★ ここから jOOQ 設定（XML ではなく Gradle DSL で書く）
jooq {
    configuration {
        // logging = org.jooq.meta.jaxb.Logging.WARN // 必要なら

        jdbc {
            driver = "org.postgresql.Driver"
            url = "jdbc:postgresql://localhost:5432/bookdb"
            user = "bookuser"
            password = "bookpass"
        }

        generator {
            name = "org.jooq.codegen.DefaultGenerator"

            database {
                name = "org.jooq.meta.postgres.PostgresDatabase"
                inputSchema = "public"
            }

            target {
                packageName = "com.example.hkoike.codingtest.bookapi.jooq"
                directory = "$buildDir/generated-sources/jooq"
            }
        }
    }
}

// 生成したクラスを main ソースとして扱う
sourceSets {
    main {
        java {
            srcDir "$buildDir/generated-sources/jooq"
        }
    }
}

// jOOQ のコード生成を Kotlin/Java のコンパイルより先に実行させる
tasks.named('compileKotlin') {
    dependsOn('jooqCodegen')
}

tasks.named('compileJava') {
    dependsOn('jooqCodegen')
}

// ktlint も jOOQ の出力を見に行くので、その前に jOOQ を動かす
tasks.named('runKtlintCheckOverMainSourceSet') {
    dependsOn('jooqCodegen')
}

tasks.named('ktlintMainSourceSetCheck') {
    dependsOn('jooqCodegen')
}
